PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX leader: <http://www.monarchinitiative.org/MONARCH_cliqueLeader>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# If ?s is a better leader than ?o, delete outgoing sameAs links from ?o
DELETE {
?o owl:sameAs ?o2 .
}
WHERE {
?s owl:sameAs ?o .
?o owl:sameAs ?o2 .
OPTIONAL {
    ?s leader: ?sleader .
}
OPTIONAL {
    ?o leader: ?oleader .
}
# More than one term in a clique can have a leader annotation. Obtain a 
# true or false value for both being compared.
BIND(COALESCE(SAMETERM(?sleader, "true"^^xsd:), false) AS ?sleaderbool)
BIND(COALESCE(SAMETERM(?oleader, "true"^^xsd:), false) AS ?oleaderbool)
# Determine whether the terms being compared have a priority IRI namespace
BIND(COALESCE(
IF (STRSTARTS(STR(?s), "http://www.ncbi.nlm.nih.gov/gene/"), 0, 
IF (STRSTARTS(STR(?s), "http://www.ncbi.nlm.nih.gov/pubmed/"), 1, 
IF (STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/NCBITaxon_"), 2, 
IF (STRSTARTS(STR(?s), "http://identifiers.org/ensembl/"), 3, 
IF (STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/OMIM_"), 4, 
IF (STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/DOID_"), 5, 
IF (STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/Orphanet_"), 6, 
IF (STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/HP_"), 7, 
IF (STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/MP_"), 8, 
IF (STRSTARTS(STR(?s), "http://purl.obolibrary.org/obo/ZP_"), 9, 
10)))))))))), 10) AS ?spriorityprefix)
BIND(COALESCE(
IF (STRSTARTS(STR(?o), "http://www.ncbi.nlm.nih.gov/gene/"), 0, 
IF (STRSTARTS(STR(?o), "http://www.ncbi.nlm.nih.gov/pubmed/"), 1, 
IF (STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/NCBITaxon_"), 2, 
IF (STRSTARTS(STR(?o), "http://identifiers.org/ensembl/"), 3, 
IF (STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/OMIM_"), 4, 
IF (STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/DOID_"), 5, 
IF (STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/Orphanet_"), 6, 
IF (STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/HP_"), 7, 
IF (STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/MP_"), 8, 
IF (STRSTARTS(STR(?o), "http://purl.obolibrary.org/obo/ZP_"), 9, 
10)))))))))), 10) AS ?opriorityprefix)
# Allow this update only if ?s is a better leader than ?o
FILTER(
    # If ?s is leader and ?o is not, allow.
    (?sleaderbool && !?oleaderbool) || 
    ( # Else if they have the same leader type, then...
        ((?sleaderbool && ?oleaderbool) || (!?sleaderbool && !?oleaderbool)) && 
        ( # If ?s has higher priority namespace, allow.
            (?spriorityprefix < ?opriorityprefix) ||
            ( # Else if they have the same namespace priority, then...
                (?spriorityprefix = ?opriorityprefix) && 
                ( # If ?s is an IRI and ?o is a blank node, allow.
                    (isIRI(?s) && isBlank(?o)) || 
                    ( # Else if both are IRIs or both are blank, then if ?s is alphabetically less than ?o, allow.
                        ((isIRI(?s) && isIRI(?o)) || (isBlank(?s) && isBlank(?o))) && (STR(?s) < STR(?o))
                    )
                )
            )
        )
    )
)
}
